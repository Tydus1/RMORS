//===== Hercules Script ======================================
//= Universal Rental NPC
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.2
//===== Description: =========================================
//= One-click automatic mount rentals.
//= Replaced 'close' with 'end' [Streusel]
//= Fixed 'close' issues. [Joseph]
//============================================================

prontera,128,214,6	script	Dan o adestrador	4_EP16_HELMUT,{
	
	if (hascashmount()) {
		message strcharinfo(PC_NAME),"Você precisa remover sua montaria antes de obter um falcão/peco-peco.";
		end;
	} else if ((eaclass()&EAJ_THIRDMASK) == EAJ_RANGER && !countitem(Wolfs_Flute)) {
		if (!checkfalcon() && getskilllv(HT_FALCON) && !checkwug()) {
			if (select(" ~ Falcon", " ~ Warg") == 1)
				setfalcon;
			else
				getitem Wolfs_Flute, 1;
			specialeffect(EF_TEIHIT3, AREA, playerattached());
			close;
		} else {
			getitem Wolfs_Flute,1;
		}
	} else if ((eaclass()&EAJ_THIRDMASK) == EAJ_MECHANIC && !checkcart() && getskilllv(MC_PUSHCART)) {
		if (!checkmount() != MOUNT_MADO && getskilllv(NC_MADOLICENCE)) {
			if (select(" ~ Cart", " ~ Mado") == 1) {
				setcart;
			} else {
				setmount(MOUNT_MADO);
			}
			specialeffect(EF_TEIHIT3, AREA, playerattached());
			close;
		} else {
			setcart;
		}
	} else if (!checkcart() && getskilllv(MC_PUSHCART)) {
		setcart;
	} else if (!checkfalcon() && getskilllv(HT_FALCON) && !checkwug()) {
		setfalcon;
	} else if (checkmount() != MOUNT_PECO && getskilllv(KN_RIDING)) {
		setmount; // Peco or Dragon
	} else if (checkmount() != MOUNT_MADO && getskilllv(NC_MADOLICENCE)) {
		setmount(MOUNT_MADO);
	} else {
		message strcharinfo(PC_NAME),"Você não possui os requerimentos para ter um falcão/peco-peco.";
		end;
	}
	specialeffect(EF_TEIHIT3, AREA, playerattached());
	end;
}

//============================================================
// Old changelog
//============================================================
//= A Free PecoPeco and Falcon Breeder
//= 1.1 FIXED checkriding/cart/falcon funcs [Lupus]
//= 1.2 Added Rebirth/Advanced Class support [Mass Zero]
//= 1.3 Simplified the checks of job [Silentdragon]
//= 1.3a Fixed a Typo Error. [Samuray22]
//=      Thanks to ~AnnieRuru~.
//= 1.4 Optimized Script. No need for 50 million Labels. [Spre]
//============================================================
